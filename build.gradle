plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.2'
	id 'io.spring.dependency-management' version '1.1.5'
	id 'org.jooq.jooq-codegen-gradle' version '3.19.10'
	id 'org.liquibase.gradle' version '2.2.2'
}

group = 'dev.abarmin'
version = '0.0.1-SNAPSHOT'

apply plugin: 'org.liquibase.gradle'

java {
	sourceCompatibility = '17'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	set('archunitVersion', '1.2.1')
	set('rssReaderVersion', '3.5.0')
	set('telegramBotApiVersion', '6.9.1')
	set('twitterBootstrapVersion', '5.2.3')
	set('googleGuavaVersion', '31.0.1-jre')
	set('select2Version', '4.0.13')
	set('jooq.version', '3.19.7')
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-jooq'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

	implementation 'org.liquibase:liquibase-core'
	implementation "com.apptasticsoftware:rssreader:${rssReaderVersion}"
	implementation "com.github.pengrad:java-telegram-bot-api:${telegramBotApiVersion}"

	implementation "org.webjars.npm:github-com-twbs-bootstrap:${twitterBootstrapVersion}"
	implementation "org.webjars.npm:select2:${select2Version}"
	implementation "org.webjars.npm:select2-bootstrap-5-theme:1.3.0"
	implementation "org.webjars.npm:jquery:3.7.1"

	implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect'
	implementation "com.google.guava:guava:${googleGuavaVersion}"

	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.h2database:h2'

	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

	'org.projectlombok:lombok'.with {
		compileOnly it
		testCompileOnly it
		annotationProcessor it
		testAnnotationProcessor it
	}

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation "com.tngtech.archunit:archunit:${archunitVersion}"

	jooqCodegen "com.h2database:h2:2.3.230"

	liquibaseRuntime "org.liquibase:liquibase-core:4.29.0"
	liquibaseRuntime "info.picocli:picocli:4.7.6"
	liquibaseRuntime "org.yaml:snakeyaml:2.2"
	liquibaseRuntime "com.h2database:h2:2.3.230"
}

jooq {
	configuration {
		jdbc {
			driver = 'org.h2.Driver'
			url = "jdbc:h2:${projectDir}/database"
			user = 'sa'
			password = 'sa'
		}
		generator {
			database {
				name = 'org.jooq.meta.h2.H2Database'
				inputSchema = 'PUBLIC'
				excludes = """
					DATABASECHANGELOG | DATABASECHANGELOGLOCK
					"""
			}
			target {
				packageName = 'dev.abarmin.bots.entity.jooq'
				directory = 'src/main/java'
			}
		}
	}
}

liquibase {
	activities {
		main {
			changelogFile "db/changelog/db.changelog-master.xml"
			url "jdbc:h2:${projectDir}/database"
			username 'sa'
			password 'sa'
			classpath "src/main/resources"
		}
	}
}

tasks.named('jooqCodegen') {
	dependsOn('update')
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.named('bootJar') {
	archiveFileName = 'app.jar'
}